; =========================================================================
; Created by ATW.
; User: VincentLin
; Date: 2017/02/16
; Time: 
; Chip: AT8B062D
; 说明：PA1: RFC Input（R=50K / C=104）
;	PB0: Timer0 Output
;	PB1: Timer2 Ouptut
;	在100MS后，读取RFC的资料，将其值存入Timer2当计数参数。
;	改变电阻，Timer2的计数时间耕著变化。电阻越大，时间越快。
; =========================================================================

	#include		AT8B62.H	
		
		
;;=========== User Register Define (0x20~0x7F) ================================
	R_ACC_Buf	EQU		0x20
	R_Status_Buf	EQU		0x21
	
	R_DelayCount	EQU		0x22
	
	R_RFCLB		EQU		0x30
	R_RFCHB		EQU		0x31
	R_RFC_Temp	EQU		0x32
	
	R_Flag0		EQU		0x33
	R_100ms_Count	EQU		0x34
	R_1s_Count	EQU		0x35

		
		

;; Chip Reset Vector
; ===============================================================
		ORG	0x00
Reset:
		GOTO	Main_Prog

;; ISR Vector
		ORG	0x08
ISR_Prog:
		MOVAR		R_ACC_Buf		;; 保留 ACC 及 Status 的状态值
		SWAPR		R_ACC_Buf, R
		SWAPR		STATUS, A
		MOVAR		R_Status_Buf

		BTRSS		INTF, T2IF_B
		GOTO		ISR_Prog100
		MOVIA		0xDF
		MOVAR		INTF			;; CLR TIMER2 FLAG
		MOVIA		0x02
		XORAR		PB, R


	ISR_Prog100:		
		BTRSS		INTF, T1IF_B
		GOTO		ISR_Prog200
		MOVIA		0xF7
		MOVAR		INTF			;; CLR TIMER1 FLAG
		INCR		R_RFC_Temp, R

	
	ISR_Prog200:
		BTRSS		INTF, T0IF_B
		GOTO		ISR_Prog300
		MOVIA		0xFE
		MOVAR		INTF			;; CLR TIMER0 FLAG
		MOVIA		0x01
		XORAR		PB, R
		BSR		R_Flag0, T0IF_B
		INCR		R_100ms_Count, R

	ISR_Prog300:
		BTRSS		INTE2, T3IF_B
		GOTO		ISR_End
		MOVIA		0xEF
		MOVAR		INTF			;; CLR TIMER3 FLAG
	

	ISR_End:
		SWAPR		R_Status_Buf, A		;; 取回 ACC	及 Status 的状态值
		MOVAR		STATUS
		SWAPR		R_ACC_Buf, A
		RETIE
		
		

;; Main Program
;		ORG	0x10
Main_Prog:
		movia		0x00
		movar		pcon
		
		LCALL		F_ClearAllRAM
		LCALL		F_IOInit
		
		MOVIA		0x29			;; ENABLE T0/T1/T2 Interrupt
		MOVAR		INTE
		
		ENI					;; Enable Goble Interrupt
		


Main_Start:
		CLRWDT

		BTRSS		R_Flag0, T0IF_B
		LGOTO		$-1
		
		BCR		R_Flag0, T0IF_B
		MOVIA		100
		SUBAR		R_100ms_Count, A
		BTRSS		STATUS, C_B
		LGOTO		Main_Start
		
		CLRR		R_100ms_COunt

		LCALL		F_RFC
		LGOTO		Main_Start
		


F_RFC:
;; RFC 设定
	; set RFC Pin	
		MOVIA		B'00000001'		;; Set PA.1 is RFC pin
		MOVAR		RFC
	
	; Set RFC Pin is Low	
		IOSTR		IOSTA			;; Set RFC Pin is Output Low
		ANDIA		B'1111101'
		IOST		IOSTA		
		BCR		PA, 1

	; Delay for Wait PA1 is Low			;; Wait Capacitive discharge
		DECRSZ		R_DelayCount, R
		LGOTO		$-1
						
		BTRSC		PA, 1
		LGOTO		$-1
		
	;; Disable Timer1	
		MOVIA		0x00
		SFUN		T1CR1			;; Disable Timer1
		MOVIA		0xF7
		MOVAR		INTF			;; Clr Timer1 Flag

	;; Set Timer1 Prescaler=1:2
		MOVIA		0x00
		SFUN		T1CR2			;; P1SEl(B2:B0) = 1:2 = 000 

	;clear tiemr1 counter (Timer1=0x3ff)
		CLRR		R_RFC_Temp
		MOVIA		0x30			;; Timer1 Data Byte [9:8]
		IORAR		TMRH, A
		MOVAR		TMRH
		MOVIA		0xff			;; Timer1 Data Byte Data[7:0]
		SFUN		TMR1

	; Clear RFC Register
		CLRR		R_RFCLB
		CLRR		R_RFCHB
		CLRR		R_RFC_Temp

	;; Enable RFC Function
		BSR		RFC, RFCEN_B		;; Enable RFC

	; Set RFC Pin is Input
		IOSTR		IOSTA			;; Set RFC Pin is Input
		IORIA		B'00000010'
		IOST		IOSTA
	
	loop:
		CLRWDT
		BTRSS		PA, 1
		LGOTO		loop

	;; Disable RFC Funtion	
		BCR		RFC, RFCEN_B		;; Disable RFC
	
		SFUNR		TMR1
		MOVAR		R_RFCLB
		CLRR		R_RFCHB
		BTRSC		TMRH, TMR18_B
		BSR		R_RFCHB, TMR28_B
		BTRSC		TMRH, TMR19_B
		BSR		R_RFCHB, TMR29_B
		
		
		MOVIA		0x00			;; TIMER1(B0):1:EN/0:Dis, AUTO ReLoad(B1):1:En/0:Dis, PWM1OAL(b6):1:PWM1 Out Low / 0: PWM1 Our Hi, PWM1oEN(B7):1:P3 is PWM1 Out / 0:P3 is GPIO
		SFUN		T2CR1		
		MOVR		R_RFCHB, A
		MOVAR		TMRH
		MOVR		R_RFCLB, A
		SFUN		TMR2
		MOVIA		0x03			;; TIMER1(B0):1:EN/0:Dis, AUTO ReLoad(B1):1:En/0:Dis, PWM1OAL(b6):1:PWM1 Out Low / 0: PWM1 Our Hi, PWM1oEN(B7):1:P3 is PWM1 Out / 0:P3 is GPIO
		SFUN		T2CR1

		RET

;==================================================================
; Project Name:	清零全部RAM
; Last Modify Date:2010-11-9 16:12
;
;;Clear	RAM 0x20~0x3f
F_ClearAllRAM:
	;Bank0	
		MOVIA		0x20				;; 切换到 RAM Bank0
		MOVAR		FSR
	L_Clear_Next_Byte_Bank0:
		CLRR		INDF				;; 清除该地址资料
		INCR		FSR, R				;; 累加间接地址
		BTRSS		FSR, 7
		GOTO		L_Clear_Next_Byte_Bank0		;; 否，跳跃
		
		RET


;====================================================================
; Project Name: IO 初始化
; Madify Data: 2014.12.11
;
F_IOInit:
		MOVIA		0xff
		MOVAR		PA
		MOVAR		PB				;; 初始值
		
		MOVIA		00000000
		IOST		PA
		IOST		PB				;; 设定为IO状态	0=Output,  1=Input
		
		MOVIA		0x80				;; 设定为 WDT(B7)=1:EN/0:DIS, LVDEN(B5)=1:EN/0:DIS, LVREN(B3)=1:EN/0:DIS
		MOVAR		PCON

;====================================================================
; Project Name: Time0 初始化
; Madify Data: 2014.12.11
; T=（1/intruction clk)*分频*TMR0
;  = ((1/4MHz)*4)*4*256(00H)
;  = 1.024ms
F_Timer0Set:
		MOVIA		0xFE
		MOVAR		INTF			;; Clr Timer0 Flag
		MOVIA		00000001
		T0MD	
		MOVIA		0x00
		MOVAR		TMR0

	
;====================================================================
; Project Name: Time1 初始化
; Madify Data: 2014.12.11
; T=（1/intruction clk)*分频*(TMR[9:8]:TMR1[7:0])
;  = ((1/4MHz)*4)*2*0ffh
;  = 510us
;		MOVIA		00000000
;		SFUN		T1CR1			;; Disale Timer1
;		
;		MOVIA		0xF7
;		MOVAR		INTF			;; Clr Timer1 Flag
;
;	
;		BCR		TMRH, TMR18_B		;; Timer1 Data Bit[8]
;		BCR		TMRH, TMR19_B		;; Timer1 Data Bit[9]
;		MOVIA		0x00			;; Timer1 Data Byte Data[7:0]
;		SFUN		TMR1
;
;
;		MOVIA		0x00
;		SFUN		T1CR2			;; P1SEl(B2:B0), PS1EN(B3):0(EN)/1(Dis), T1CE(B4):0:Low->Hi/1:hi->Low, T1CS(B5):0:Instruction CLK/1:Ext CLK
;		MOVIA		0x00			;; TIMER1(B0):1:EN/0:Dis, AUTO ReLoad(B1):1:En/0:Dis, PWM1OAL(b6):1:PWM1 Out Low / 0: PWM1 Our Hi, PWM1oEN(B7):1:P3 is PWM1 Out / 0:P3 is GPIO
;		SFUN		T1CR1

;====================================================================
; Project Name: Time2 初始化
; Madify Data: 2014.12.11
; T=（1/intruction clk)*分频*(TMR[9:8]:TMR1[7:0])
;  = ((1/4MHz)*4)*2*0ffh
;  = 510us
		MOVIA		00000000
		SFUN		T2CR1			;; Disale Timer1
		
		MOVIA		0xDF
		MOVAR		INTF			;; Clr Timer1 Flag


		BCR		TMRH, TMR28_B
		MOVIA		0x00			;; Timer1 Data Byte Data[7:0]
		SFUN		TMR2
		BCR		TMRH, TMR29_B
		MOVIA		0x00
		SFUN		TMR2


		MOVIA		0x00
		SFUN		T2CR2			;; P1SEl(B2:B0), PS1EN(B3):0(EN)/1(Dis), T1CE(B4):0:Low->Hi/1:hi->Low, T1CS(B5):0:Instruction CLK/1:Ext CLK
		MOVIA		0x00			;; TIMER1(B0):1:EN/0:Dis, AUTO ReLoad(B1):1:En/0:Dis, PWM1OAL(b6):1:PWM1 Out Low / 0: PWM1 Our Hi, PWM1oEN(B7):1:P3 is PWM1 Out / 0:P3 is GPIO
		SFUN		T2CR1

		RET

		
		end
;Program END



	

; =========================================================================
; Created by ATW.
; User: VincentLin
; Date: 2016/02/23
; Time: 7:38
; Chip: AT8B62D
; 说明：PB3 口为 PWM1 功能输出测试。PB2 口为 PWM2 功能输出测试。PA2 口为 PWM3 功能输出测试。(PB0输出只是验证MCU是否有动作）
; =========================================================================

	#include	AT8B62.h
		
;; User Register Define (0x20~0x7F)
	R_ACC_Buf	EQU		0x20
	R_Status_Buf	EQU		0x21
	

;; Chip Reset Vector
; ===============================================================
		ORG	0x00
Reset:
		GOTO	Main_Prog

;; ISR Vector
		ORG	0x08
ISR_Prog:
		MOVAR		R_ACC_Buf			;; 保留 ACC 及 Status 的状态值
		SWAPR		R_ACC_Buf, R
		SWAPR		STATUS, A
		MOVAR		R_Status_Buf
	
	ISR_Prog100:
		BTRSS		INTF, T0IF_B
		GOTO		ISR_End
;		BCR			INTF, T0IF		;; 不建议使用方式清除
		MOVIA		0xFE
		MOVAR		INTF			;; CLR TIMER0 FLAG
		MOVIA		0x01
		XORAR		PB, R

	ISR_End:
		SWAPR		R_Status_Buf, A		;; 取回 ACC	及 Status 的状态值
		MOVAR		STATUS
		SWAPR		R_ACC_Buf, A
		RETIE
		
		

;; Main Program
;		ORG	0x10
Main_Prog:
		LCALL		F_ClearAllRAM
		LCALL		F_IOInit
		
		MOVIA		0x01			;; ENABLE T0 Interrupt
		MOVAR		INTE
		
		ENI							;; Enable Goble Interrupt

Main_Start:
		CLRWDT
		GOTO		Main_Start

;==================================================================
; Project Name:	清零全部RAM
; Last Modify Date:2010-11-9 16:12
;
;;Clear	RAM 0x20~0x7f
F_ClearAllRAM:
	;Bank0	
		MOVIA		0x20			;; 切换到 RAM Bank0
		MOVAR		FSR
	L_Clear_Next_Byte_Bank0:
		CLRR		INDF			;; 清除该地址资料
		INCR		FSR, R			;; 累加间接地址
		BTRSS		FSR, 7
		GOTO		L_Clear_Next_Byte_Bank0		;; 否，跳跃
		
		RET

;====================================================================
; Project Name: IO 初始化
; Madify Data: 2014.12.11
;
F_IOInit:
		MOVIA		0xff
		MOVAR		PB				;; 初始值
		MOVIA		00000000
		IOST		PB				;; 设定为IO状态	0=Output,  1=Input
		
		MOVIA		0x80			;; 设定为 WDT(B7)=1:EN/0:DIS, PB0(B6)=1:INT/0:GPIO, LVR(B3)=1:EN/0:DIS
		MOVAR		PCON

;====================================================================
; Project Name: Time0 初始化
; Madify Data: 2014.12.11
; T=（1/intruction clk)*分频*TMR0
;  = ((1/4MHz)*4)*4*256(00H)
;  = 1.024ms
F_Timer0Set:
		MOVIA		0xFE
		MOVAR		INTF			;; Clr Timer0 Flag
		MOVIA		00000001
		T0MD	
		MOVIA		0x00
		MOVAR		TMR0

;====================================================================
; Project Name: PWM1 初始化
; Madify Data: 2014.12.11
; PWM1 Frequecy=(1/H_IRC)*指令分频*时间分频＊(TMR1+1)
;             =(1/4MHz)*4T*256*(080H+1)
;             = 33.024ms
;
; Duty1 = (1/H_IRC)*指令分频*时间分频＊PWM1DUTY
;      = (1/4)*4*256*0aH
;      = 2.56ms
;
		MOVIA		00000000
		SFUN		T1CR1			;; Disale Timer1
		MOVIA		0xF7
		MOVIA		INTF			;; Clr Timer1 Flag

		MOVIA		0x00			;; Timer1 Data Bit[9:8]=0
		MOVAR		TMRH
		MOVIA		0x80			;; Timer1 Data Byte Data[7:0]
		SFUN		TMR1


		MOVIA		0x07
		SFUN		T1CR2			;; P1SEl(B2:B0), PS1EN(B3):0(EN)/1(Dis), T1CE(B4):0:Low->Hi/1:hi->Low, T1CS(B5):0:Instruction CLK/1:Ext CLK

		BCR		TMRH, PWM1DUTY8_B
		BCR		TMRH, PWM1DUTY9_B
		MOVIA		0x0A
		SFUN		PWM1DUTY

		
		MOVIA		0x83			;; TIMER1(B0):1:EN/0:Dis, AUTO ReLoad(B1):1:En/0:Dis, PWM1oEN(B7):1:P2 is PWMO/0:P2 is GPIO
		SFUN		T1CR1

;====================================================================
; Project Name: PWM2 初始化
; Madify Data: 2016.02.20
; PWM2 Frequecy	=(1/H_IRC)*指令分频*时间分频＊(TMR2+1)
;  		=((1/4MHz)*4)*2*180H
;		=768us

; Duty2	= (1/H_IRC)*指令分频*时间分频＊PWM2DUTY
;      	= (1/4)*4*2*110H
;      	= 544us
;
		MOVIA		00000000
		SFUN		T2CR1			;; Disale Timer2
		
		MOVIA		0xDF
		MOVAR		INTF			;; Clr Timer2 Flag
		
		MOVIA		0x40			;; Timer2 Data Bit[9:8]=01
		MOVAR		TMRH
		MOVIA		0x80
		SFUN		TMR2

		BSR		TMRH, PWM2DUTY8_B	;; Time2 Duty bit[9:8]=01
		BCR		TMRH, PWM2DUTY9_B
		MOVIA		0x10
		SFUN		PWM2DUTY


		MOVIA		0x00
		SFUN		T2CR2			;; P2SEl(B2:B0), PS2EN(B3):0(EN)/1(Dis), T2CE(B4):0:Low->Hi/1:hi->Low, T2CS(B5):0:Instruction CLK/1:Ext CLK

		MOVIA		0x83			;; T2EN(B0):1:EN Timer2/0:Dis, T2RL(B1):1:En Auto Load/0:Dis, T2OS(B2):1:One-Shot/0:No_Stop, PWM2OAL(B6):1:PWM2 active Low/0:High, PWM2oEN(B7):1:PB2 is PWM2 / 0:PB2 is GPIO
		SFUN		T2CR1

;====================================================================
; Project Name: Time3 初始化
; Madify Data: 2016.02.20
; PWM3 Frequecy	= (1/H_IRC)*指令分频*时间分频＊(TMR3+1)
;  		= ((1/4MHz)*4)*2*280H
;  		= 1.28ms
;
; Duty3	= (1/H_IRC)*指令分频*时间分频＊PWM3DUTY
;      	= (1/4)*4*2*20H
;      	= 64us
;
		MOVIA		00000000
		SFUN		T3CR1			;; Disale Timer3
		
		MOVIA		0xEE
		MOVAR		INTE2			;; Clr Timer3 Flag
		
		MOVIA		0x20			;; Timer3 Data Bit[9:8]=2
		MOVAR		TM3RH
		MOVIA		0x80
		SFUN		TMR3

		
		MOVIA		0x00
		SFUN		T3CR2			;; P3SEl(B2:B0), PS3EN(B3):0(EN)/1(Dis), T3CE(B4):0:Low->Hi/1:hi->Low, T3CS(B5):0:Instruction CLK/1:Ext CLK

		BCR		TM3RH, PWM3D8_B
		BCR		TM3RH, PWM3D9_B
		MOVIA		0x20
		SFUN		PWM3DUTY

		MOVIA		0x83			;; TIMER3(B0):1:EN/0:Dis, AUTO ReLoad(B1):1:En/0:Dis, PWM3oEN(B7):1:PA2 is PWM2 / 0:PA2 is GPIO
		SFUN		T3CR1

		RET

		end
;Program END


